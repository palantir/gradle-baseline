apply plugin: 'groovy'
apply plugin: 'java-library'
apply plugin: 'com.palantir.external-publish-jar'
apply plugin: 'com.palantir.external-publish-gradle-plugin'

import org.gradle.api.internal.classpath.ModuleRegistry
import org.gradle.api.internal.project.ProjectInternal

dependencies {
    implementation gradleApi()
    implementation 'org.apache.commons:commons-lang3'
    implementation 'com.diffplug.spotless:spotless-plugin-gradle'
    implementation 'com.github.ben-manes.caffeine:caffeine'
    implementation 'com.google.guava:guava'
    implementation 'net.ltgt.gradle:gradle-errorprone-plugin'
    implementation 'org.apache.maven.shared:maven-dependency-analyzer', {
        // Use maven-core rather than the legacy maven-project component
        exclude group: 'org.apache.maven', module: 'maven-project'
    }
    runtimeOnly 'org.apache.maven:maven-core'

    implementation 'commons-lang:commons-lang'
    implementation 'com.palantir.javaformat:palantir-java-format-spi'
    // Add an explicit dependency to ensure consumers can use JDK14 source compat
    implementation 'org.ow2.asm:asm'
    implementation 'com.googlecode.java-diff-utils:diffutils'
    implementation 'com.palantir.gradle.utils:lazily-configured-mapping'
    implementation 'com.palantir.gradle.failure-reports:gradle-failure-reports-exceptions'

    runtimeOnly 'com.palantir.javaformat:gradle-palantir-java-format'

    testImplementation gradleTestKit()
    testImplementation 'com.github.stefanbirkner:system-rules'
    testImplementation 'com.netflix.nebula:nebula-test' // for better temp directory junit rule only
    testImplementation 'commons-io:commons-io'
    testImplementation 'junit:junit'
    testImplementation 'net.lingala.zip4j:zip4j'
    testImplementation 'org.assertj:assertj-core'
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.vintage:junit-vintage-engine', {
        because 'allows JUnit 3 and JUnit 4 tests to run'
    }
    // necessary because of https://github.com/gradle/gradle/issues/16774
    def toolingApiBuildersJar = (project as ProjectInternal).services.get(ModuleRegistry.class)
            .getModule("gradle-tooling-api-builders")
            .classpath
            .asFiles
            .first()
    testRuntimeOnly(files(toolingApiBuildersJar))

    annotationProcessor 'org.inferred:freebuilder'
    compileOnly 'org.inferred:freebuilder'
    annotationProcessor 'org.immutables:value'
    compileOnly 'org.immutables:value::annotations'
}

tasks.test.dependsOn tasks.findByPath(':gradle-baseline-java-config:publishToMavenLocal')
tasks.test.dependsOn tasks.findByPath(':baseline-error-prone:publishToMavenLocal')
tasks.test.dependsOn tasks.findByPath(':baseline-null-away:publishToMavenLocal')
tasks.test.dependsOn tasks.publishToMavenLocal

test {
    environment 'CIRCLE_ARTIFACTS', "${buildDir}/artifacts"
    environment 'CIRCLE_TEST_REPORTS', "${buildDir}/circle-reports"
    environment 'JAVA_TOOL_OPTIONS', "--add-exports=jdk.compiler/com.sun.tools.javac.api=ALL-UNNAMED --add-exports=jdk.compiler/com.sun.tools.javac.tree=ALL-UNNAMED --add-exports=jdk.compiler/com.sun.tools.javac.parser=ALL-UNNAMED --add-exports=jdk.compiler/com.sun.tools.javac.file=ALL-UNNAMED --add-exports=jdk.compiler/com.sun.tools.javac.util=ALL-UNNAMED"

    systemProperty 'ignoreDeprecations', 'true'
}

gradlePlugin {
    website = 'https://github.com/palantir/gradle-baseline'
    vcsUrl = 'https://github.com/palantir/gradle-baseline'

    plugins {
        baselinePlugins {
            id = 'com.palantir.baseline'
            displayName = 'Palantir Baseline Plugins'
            implementationClass = 'com.palantir.baseline.plugins.Baseline'
            description = 'Baseline Java is a collection of Gradle plugins for configuring code quality tools in builds and generated Eclipse/IntelliJ projects.'
            tags.set(['java', 'checkstyle', 'code quality', 'eclipse', 'idea'])
        }
        baselineCheckstylePlugin {
            id = 'com.palantir.baseline-checkstyle'
            displayName = 'Palantir Baseline Checkstyle Plugin'
            implementationClass = 'com.palantir.baseline.plugins.BaselineCheckstyle'
            description = 'Baseline Java is a collection of Gradle plugins for configuring code quality tools in builds.'
            tags.set(['java', 'checkstyle', 'code quality'])
        }
        baselineConfigPlugin {
            id = 'com.palantir.baseline-config'
            displayName = 'Palantir Baseline Configuration Plugin'
            implementationClass = 'com.palantir.baseline.plugins.BaselineConfig'
            description = 'Baseline Java is a collection of Gradle plugins for configuring code quality tools in builds.'
            tags.set(['java', 'checkstyle', 'code quality'])
        }
        baselineErrorPronePlugin {
            id = 'com.palantir.baseline-error-prone'
            displayName = 'Palantir Baseline Error Prone Plugin'
            implementationClass = 'com.palantir.baseline.plugins.BaselineErrorProne'
            description = 'Baseline Java is a collection of Gradle plugins for configuring code quality tools in builds.'
            tags.set(['java', 'checkstyle', 'code quality'])
        }
        baselineExactDependenciesPlugin {
            id = 'com.palantir.baseline-exact-dependencies'
            displayName = 'Palantir Baseline Exact Dependencies Plugin'
            implementationClass = 'com.palantir.baseline.plugins.BaselineExactDependencies'
            description = 'Baseline Java is a collection of Gradle plugins for configuring code quality tools in builds.'
            tags.set(['java', 'checkstyle', 'code quality'])
        }
        baselineEclipsePlugin {
            id = 'com.palantir.baseline-eclipse'
            displayName = 'Palantir Baseline Eclipse Plugin'
            implementationClass = 'com.palantir.baseline.plugins.BaselineEclipse'
            description = 'Baseline Java is a collection of Gradle plugins for configuring code quality tools in builds.'
            tags.set(['java', 'checkstyle', 'code quality', 'eclipse'])
        }
        baselineIdeaPlugin {
            id = 'com.palantir.baseline-idea'
            displayName = 'Palantir Baseline IntelliJ Plugin'
            implementationClass = 'com.palantir.baseline.plugins.BaselineIdea'
            description = 'Baseline Java is a collection of Gradle plugins for configuring code quality tools in builds.'
            tags.set(['java', 'checkstyle', 'code quality', 'idea'])
        }
        baselineClassUniquenessPlugin {
            id = 'com.palantir.baseline-class-uniqueness'
            displayName = 'Palantir Baseline Class Uniqueness Plugin'
            implementationClass = 'com.palantir.baseline.plugins.BaselineClassUniquenessPlugin'
            description = 'Baseline Java is a collection of Gradle plugins for configuring code quality tools in builds.'
            tags.set(['java', 'checkstyle', 'code quality'])
        }
        baselineCircleCiPlugin {
            id = 'com.palantir.baseline-circleci'
            displayName = 'Palantir Baseline CircleCi Plugin'
            implementationClass = 'com.palantir.baseline.plugins.BaselineCircleCi'
            description = 'Baseline Java is a collection of Gradle plugins for configuring code quality tools in builds.'
            tags.set(['java', 'checkstyle', 'code quality'])
        }
        baselineReleaseCompatibility {
            id = 'com.palantir.baseline-release-compatibility'
            displayName = 'Palantir Baseline Release Compatibility Plugin'
            implementationClass = 'com.palantir.baseline.plugins.BaselineReleaseCompatibility'
            description = 'Baseline Java is a collection of Gradle plugins for configuring code quality tools in builds.'
            tags.set(['java', 'checkstyle', 'code quality'])
        }
        baselinePreferProjectModules {
            id = 'com.palantir.baseline-prefer-project-modules'
            displayName = 'Palantir Baseline Prefer Project Modules Plugin'
            implementationClass = 'com.palantir.baseline.plugins.BaselinePreferProjectModules'
            description = 'Baseline Java is a collection of Gradle plugins for configuring code quality tools in builds.'
            tags.set(['java', 'checkstyle', 'code quality'])
        }
    }
}

tasks.named('processResources').configure {
    duplicatesStrategy 'include'
}

// Run `./gradlew test -Drecreate=true` to recreate all the expected
// generated code that we have checked into the repo.
tasks.withType(Test) {
    systemProperty 'recreate', System.getProperty('recreate', 'false')
}

// TODO(esword): turn off a bunch of checks until we come back and fix.  The code was compiled by the groovy compiler
// and so none of these checks were active until we switched to the java compiler.
pluginManager.withPlugin('java') {
    tasks.withType(JavaCompile) {
        options.compilerArgs = (options.compilerArgs - '-Xlint:deprecation')
        options.errorprone {
            disable("BadImport")
            disable("DefaultCharset")
            disable("FinalClass")
            disable("ImmutableMapDuplicateKeyStrategy")
            disable("ImmutablesStyle")
            disable("JavaTimeDefaultTimeZone")
            disable("LambdaMethodReference")
            disable("LenientFormatStringValidation")
            disable("MissingOverride")
            disable("OptionalFlatMapOfNullable")
            disable("StreamResourceLeak")
            disable("StrictUnusedVariable")
        }
    }
}

configurations {
    checkstyleVersion
}

dependencies {
    checkstyleVersion 'com.puppycrawl.tools:checkstyle'
}

task writeCheckstyleVersion() {
    doLast {
        file('src/main/resources/checkstyle.version').text = getVersion('com.puppycrawl.tools', 'checkstyle', configurations.checkstyleVersion)
    }
}

if (gradle.startParameter.isWriteDependencyLocks()) {
    gradle.startParameter.taskNames += 'writeCheckstyleVersion'
}
