apply plugin: 'groovy'
apply plugin: 'java-library'
apply plugin: 'com.palantir.external-publish-jar'
apply plugin: 'com.palantir.external-publish-gradle-plugin'

import org.gradle.api.internal.classpath.ModuleRegistry
import org.gradle.api.internal.project.ProjectInternal

dependencies {
    implementation project(':gradle-junit-reports')
    implementation gradleApi()
    implementation 'org.apache.commons:commons-lang3'
    implementation 'com.diffplug.spotless:spotless-plugin-gradle'
    implementation 'com.github.ben-manes.caffeine:caffeine'
    implementation 'com.google.guava:guava'
    implementation 'net.ltgt.gradle:gradle-errorprone-plugin'
    implementation 'org.apache.maven.shared:maven-dependency-analyzer'
    implementation 'commons-lang:commons-lang'
    implementation 'com.palantir.javaformat:palantir-java-format-spi'
    // Add an explicit dependency to ensure consumers can use JDK14 source compat
    implementation 'org.ow2.asm:asm'
    implementation 'com.googlecode.java-diff-utils:diffutils'

    runtimeOnly 'com.palantir.javaformat:gradle-palantir-java-format'

    testImplementation gradleTestKit()
    testImplementation 'com.github.stefanbirkner:system-rules'
    testImplementation 'com.netflix.nebula:nebula-test' // for better temp directory junit rule only
    testImplementation 'junit:junit'
    testImplementation 'net.lingala.zip4j:zip4j'
    testImplementation 'org.assertj:assertj-core'
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.vintage:junit-vintage-engine', {
        because 'allows JUnit 3 and JUnit 4 tests to run'
    }
    // necessary because of https://github.com/gradle/gradle/issues/16774
    def toolingApiBuildersJar = (project as ProjectInternal).services.get(ModuleRegistry.class)
            .getModule("gradle-tooling-api-builders")
            .classpath
            .asFiles
            .first()
    testRuntimeOnly(files(toolingApiBuildersJar))

    annotationProcessor 'org.inferred:freebuilder'
    compileOnly 'org.inferred:freebuilder'
    annotationProcessor 'org.immutables:value'
    compileOnly 'org.immutables:value::annotations'
}

tasks.test.dependsOn tasks.findByPath(':gradle-baseline-java-config:publishToMavenLocal')
tasks.test.dependsOn tasks.findByPath(':baseline-error-prone:publishToMavenLocal')
tasks.test.dependsOn tasks.findByPath(':baseline-refaster-javac-plugin:publishToMavenLocal')
tasks.test.dependsOn tasks.findByPath(':baseline-refaster-rules:publishToMavenLocal')
tasks.test.dependsOn tasks.publishToMavenLocal

test {
    environment 'CIRCLE_ARTIFACTS', "${buildDir}/artifacts"
    environment 'CIRCLE_TEST_REPORTS', "${buildDir}/circle-reports"
}

gradlePlugin {
    plugins {
        baselinePlugins {
            id = 'com.palantir.baseline'
            implementationClass = 'com.palantir.baseline.plugins.Baseline'
        }
        baselineCheckstylePlugin {
            id = 'com.palantir.baseline-checkstyle'
            implementationClass = 'com.palantir.baseline.plugins.BaselineCheckstyle'
        }
        baselineConfigPlugin {
            id = 'com.palantir.baseline-config'
            implementationClass = 'com.palantir.baseline.plugins.BaselineConfig'
        }
        baselineExactDependenciesPlugin {
            id = 'com.palantir.baseline-exact-dependencies'
            implementationClass = 'com.palantir.baseline.plugins.BaselineExactDependencies'
        }
        baselineEclipsePlugin {
            id = 'com.palantir.baseline-eclipse'
            implementationClass = 'com.palantir.baseline.plugins.BaselineEclipse'
        }
        baselineIdeaPlugin {
            id = 'com.palantir.baseline-idea'
            implementationClass = 'com.palantir.baseline.plugins.BaselineIdea'
        }
        baselineClassUniquenessPlugin {
            id = 'com.palantir.baseline-class-uniqueness'
            implementationClass = 'com.palantir.baseline.plugins.BaselineClassUniquenessPlugin'
        }
        baselineCircleCiPlugin {
            id = 'com.palantir.baseline-circleci'
            implementationClass = 'com.palantir.baseline.plugins.BaselineCircleCi'
        }
        baselineReleaseCompatibility {
            id = 'com.palantir.baseline-release-compatibility'
            implementationClass = 'com.palantir.baseline.plugins.BaselineReleaseCompatibility'
        }
        baselineEnablePreviewFlag {
            id = 'com.palantir.baseline-enable-preview-flag'
            implementationClass = 'com.palantir.baseline.plugins.BaselineEnablePreviewFlag'
        }
        baselinePreferProjectModules {
            id = 'com.palantir.baseline-prefer-project-modules'
            implementationClass = 'com.palantir.baseline.plugins.BaselinePreferProjectModules'
        }
    }
}

pluginBundle {
    website = 'https://github.com/palantir/gradle-baseline'
    vcsUrl = 'https://github.com/palantir/gradle-baseline'
    description = 'Baseline Java is a collection of Gradle plugins for configuring code quality tools in builds and generated Eclipse/IntelliJ projects.'
    tags = ['java', 'checkstyle', 'code quality', 'eclipse', 'idea']

    plugins {
        baselinePlugins {
            displayName = 'Palantir Baseline Plugins'
        }
        baselineCheckstylePlugin {
            displayName = 'Palantir Baseline Checkstyle Plugin'
        }
        baselineConfigPlugin {
            displayName = 'Palantir Baseline Configuration Plugin'
        }
        baselineExactDependenciesPlugin {
            displayName = 'Palantir Baseline Exact Dependencies Plugin'
        }
        baselineEclipsePlugin {
            displayName = 'Palantir Baseline Eclipse Plugin'
        }
        baselineIdeaPlugin {
            displayName = 'Palantir Baseline IntelliJ Plugin'
        }
        baselineClassUniquenessPlugin {
            displayName = 'Palantir Baseline Class Uniqueness Plugin'
        }
        baselineCircleCiPlugin {
            displayName = 'Palantir Baseline CircleCi Plugin'
        }
        baselineReleaseCompatibility {
            displayName = 'Palantir Baseline Release Compatibility Plugin'
        }
        baselineEnablePreviewFlag {
            displayName = 'Palantir Baseline --enable-preview Flag Plugin'
        }
        baselinePreferProjectModules {
            displayName = 'Palantir Baseline Prefer Project Modules Plugin'
        }
    }
}

tasks.named('processResources').configure {
    duplicatesStrategy 'include'
}

// Run `./gradlew test -Drecreate=true` to recreate all the expected
// generated code that we have checked into the repo.
tasks.withType(Test) {
    systemProperty 'recreate', System.getProperty('recreate', 'false')
}

configurations {
    checkstyleVersion
}

dependencies {
    checkstyleVersion 'com.puppycrawl.tools:checkstyle'
}

task writeCheckstyleVersion() {
    doLast {
        file('src/main/resources/checkstyle.version').text = getVersion('com.puppycrawl.tools', 'checkstyle', configurations.checkstyleVersion)
    }
}

if (gradle.startParameter.isWriteDependencyLocks()) {
    gradle.startParameter.taskNames += 'writeCheckstyleVersion'
}
