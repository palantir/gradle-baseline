import net.ltgt.gradle.errorprone.CheckSeverity

buildscript {
    repositories {
        jcenter()
        gradlePluginPortal()
        maven { url  'https://palantir.bintray.com/releases'}
    }

    dependencies {
        classpath 'com.palantir.gradle.externalpublish:gradle-external-publish-plugin:1.0.1'
        classpath 'com.palantir.gradle.consistentversions:gradle-consistent-versions:1.28.0'
        classpath 'com.gradle.publish:plugin-publish-plugin:0.14.0'
        classpath 'com.palantir.baseline:gradle-baseline-java:3.79.0'
        classpath 'com.palantir.javaformat:gradle-palantir-java-format:2.0.1'
    }
}

plugins {
    id 'com.palantir.git-version' version '0.12.3'
    id 'org.inferred.processors' version '3.3.0'
}

apply plugin: 'com.palantir.external-publish'
apply plugin: 'com.palantir.consistent-versions'
apply plugin: 'com.palantir.baseline'

allprojects {
    repositories {
        jcenter()
        gradlePluginPortal()
        maven { url  'https://palantir.bintray.com/releases'}
    }

    // Since we are using gradle-baseline in gradle-baseline we need to undo forces on project dependencies. Following
    // snippet forces the version to be the version originally resolved by gradle and not the force that nebula forced
    // it to.
    configurations.all { c ->
        c.resolutionStrategy { preferProjectModules() }
    }

    pluginManager.withPlugin('java') {
        sourceCompatibility = 1.8
        tasks.withType(JavaCompile) {
            options.compilerArgs += ['-Werror', '-Xlint:deprecation']
            options.errorprone {
                check("Slf4jLogsafeArgs", CheckSeverity.OFF)
                check("PreferSafeLoggableExceptions", CheckSeverity.OFF)
                check("PreferSafeLoggingPreconditions", CheckSeverity.OFF)
                check("PreconditionsConstantMessage", CheckSeverity.OFF)
            }
        }
    }

    apply plugin: 'org.inferred.processors'
    apply plugin: 'com.palantir.java-format'

    group = 'com.palantir.baseline'
    version System.env.CIRCLE_TAG ?: gitVersion()
    description = 'A Gradle plugin for applying Baseline-recommended build and IDE settings'
}
