buildscript {
    repositories {
        mavenCentral() { metadataSources { mavenPom(); ignoreGradleMetadataRedirection() } }
        gradlePluginPortal() { metadataSources { mavenPom(); ignoreGradleMetadataRedirection() } }
    }

    dependencies {
        classpath 'com.palantir.jakartapackagealignment:jakarta-package-alignment:0.6.0'
        classpath 'com.palantir.gradle.jdks:gradle-jdks:0.51.0'
        classpath 'com.palantir.gradle.jdkslatest:gradle-jdks-latest:0.15.0'
        classpath 'com.diffplug.gradle:goomph:3.32.1'
        classpath 'com.palantir.gradle.externalpublish:gradle-external-publish-plugin:1.19.0'
        classpath 'com.palantir.gradle.failure-reports:gradle-failure-reports:1.13.0'
        classpath 'com.palantir.gradle.consistentversions:gradle-consistent-versions:2.27.0'
        classpath 'com.gradle.publish:plugin-publish-plugin:1.3.0'
        classpath 'com.palantir.baseline:gradle-baseline-java:5.70.0'
        classpath 'com.palantir.javaformat:gradle-palantir-java-format:2.50.0'
    }
}

plugins {
id 'com.palantir.git-version' version '3.1.0'
id 'org.inferred.processors' version '3.7.0'
}

apply plugin: 'com.palantir.external-publish'
apply plugin: 'com.palantir.failure-reports'
apply plugin: 'com.palantir.jdks'
apply plugin: 'com.palantir.consistent-versions'
apply plugin: 'com.palantir.baseline'
apply plugin: 'com.palantir.baseline-java-versions'
apply plugin: 'com.palantir.jdks.latest'

allprojects {
    repositories {
        mavenCentral() { metadataSources { mavenPom(); ignoreGradleMetadataRedirection() } }
        gradlePluginPortal()
    }

    // Since we are using gradle-baseline in gradle-baseline we need to undo forces on project dependencies. Following
    // snippet forces the version to be the version originally resolved by gradle and not the force that nebula forced
    // it to.
    configurations.all { c ->
        c.resolutionStrategy { preferProjectModules() }
    }

    pluginManager.withPlugin('java') {
        tasks.withType(JavaCompile) {
            options.compilerArgs += ['-Werror', '-Xlint:deprecation']
            options.errorprone {
                disable("Slf4jLogsafeArgs")
                disable("PreferSafeLoggableExceptions")
                disable("PreferSafeLogger")
                disable("PreferSafeLoggingPreconditions")
                disable("PreconditionsConstantMessage")
            }
        }
    }

    apply plugin: 'org.inferred.processors'
    apply plugin: 'com.palantir.java-format'
    apply plugin: 'com.palantir.jakarta-package-alignment'

    group = 'com.palantir.baseline'
    version System.env.CIRCLE_TAG ?: gitVersion()
    description = 'A Gradle plugin for applying Baseline-recommended build and IDE settings'
}

javaVersions {
    libraryTarget = 11
    runtime = 11
}

jdks {
    daemonTarget = 17
}
