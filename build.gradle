buildscript {
    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.4'
        classpath 'com.gradle.publish:plugin-publish-plugin:0.9.1'
        classpath 'com.palantir:gradle-flexversion:0.7.0'
    }
}

apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'maven-publish'

repositories {
    jcenter()
}

subprojects {
    apply plugin: 'com.jfrog.bintray'
    bintrayUpload.onlyIf {
        project.version.isTag == true
    }

    apply plugin: 'gradle-flexversion'
    flexversion {
        useTags = true
    }

    group = 'com.palantir'
    version = flexVersion()
    description = 'A Gradle plugin for applying Baseline-recommended build and IDE settings'
}

def baselineDependency = dependencies.create("com.palantir:gradle-baseline-java-config:+@zip")
def baselineConfig = configurations.detachedConfiguration(baselineDependency)
task baselineUpdateConfig {
    doLast {
        copy {
            from zipTree(baselineConfig.getSingleFile())
            into '.baseline'
        }
    }
}
